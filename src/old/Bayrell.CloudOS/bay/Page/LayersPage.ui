<!--
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
-->

<class version="1.0"
	name="Bayrell.CloudOS.Page.LayersPage" extends="Runtime.Web.CRUD.CrudPage" 
	module="Bayrell.CloudOS" model="Runtime.Web.CRUD.CrudPageModel"
>

<use name="Runtime.lib" />
<use name="Runtime.Exceptions.RuntimeException" />
<use name="Runtime.Core.Message" />
<use name="Runtime.Core.RemoteCallAnswer" />
<use name="Runtime.Web.LayoutModel" />
<use name="Runtime.Web.RenderContainer" />
<use name="Runtime.Web.RenderDriver" />
<use name="Runtime.Web.Route" />
<use name="Runtime.Web.RouteList" />
<use name="Runtime.Web.RouteMiddleware" />
<use name="Runtime.Web.Events.MouseClickEvent" />
<use name="Runtime.Web.CRUD.CrudPage" component="true" />
<use name="Runtime.Web.CRUD.CrudPageModel" />
<use name="Runtime.Web.CRUD.FieldInfo" />
<use name="Runtime.Web.Input.Input" component="true" />
<use name="Runtime.Web.Input.Label" component="true" />
<use name="Runtime.Web.Input.Select" component="true" />
<use name="Runtime.Web.Input.SelectText" component="true" />
<use name="Runtime.Web.Table.TableModel" />


<script>


/**
 * Returns object name
 */
pure string getCrudObjectName() => "Bayrell.CloudOS.Layer";



/**
 * Route Action
 * @return RenderContainer
 */
@Route{ "uri": "/admin/layers/", "name": "app.admin.layers" }
static async RenderContainer actionIndex(RenderContainer container) => [
	container
		
		/* Set title */
		|> lib::setAttr(["layout", "title"], "Layers")
		|> lib::setAttr(["layout", "layout_name"], "admin")
		
		/* Set model */
		|> lib::setAttr(["layout", "page_class"], classof LayersPage)
		|> lib::setAttr(["layout", "page_model"], new CrudPageModel())
		
		/* Make api request */
		|> await static::crudSearch
];



/**
 * Returns options
 */
pure Collection getOptions(RenderContainer container, string name = "")
{
	if (name == "spaces")
		return container.layout.page_model
			|> attr "foreigns", "spaces", "options"
			|> lib::map
			(
				Dict (Dict item) =>
				{
					"id": item["space_id"],
					"value": item["api_name"],
					"item": item,
				}
			)
			|> default Collection []
		;
		
	return parent::getOptions(container, name);
}



/**
 * Returns crud struct
 */
pure Collection<FieldInfo> getStruct(RenderContainer container) =>
[
	new FieldInfo
	{
		"api_name": "layer_uid",
		"primary": true,
		"label": "Layer UID",
		"class_name": classof Input,
		"info":
		{
			"table":
			{
				"class_name": classof Label,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "number",
		"label": "",
		"class_name": classof Label,
	},
	
	new FieldInfo
	{
		"api_name": "layer_name",
		"label": "Name",
		"class_name": classof Input,
		"info":
		{
			"table":
			{
				"class_name": classof Label,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "space_id",
		"label": "Space",
		"class_name": classof Select,
		"class_settings":
		{
			"show_select_value": true,
			"options": static::getOptions(container, "spaces"),
		},
		"info":
		{
			"table":
			{
				"class_name": classof SelectText,
			},
		},
	},
	
	new FieldInfo
	{
		"api_name": "edit-buttons",
		"label": "",
		"class_name": classof Label,
	},
];



/**
 * Returns filter fields
 */
pure Collection<string> getFilterFields(RenderContainer container) =>
[
	"space_id",
	"layer_uid",
];



/**
 * Returns form fields
 */
pure Collection<string> getFormFields(RenderContainer container) =>
[
	"space_id",
	"layer_name",
	"layer_uid",
];



/**
 * Returns table fields
 */
pure Collection<string> getTableFields(RenderContainer container) =>
[
	"number",
	"space_id",
	"layer_name",
	"layer_uid",
	"edit-buttons"
];



/**
 * Returns view fields
 */
pure Collection<string> getViewFields(RenderContainer container) =>
[
];



/**
 * Returns messages
 */
pure Dict<string> getMessages() =>
{
	"add": _("Bayrell.TimePlanner", "Add layer"),
	"delete": string (Dict item) =>
		_("Runtime.Web.CRUD", "Do you realy want to delete '%name%' ?", { "name": item["layer_name"] } ),
};



/**
 * Dialog settings
 */
pure Dict getDialogSettings() =>
{
};



/**
 * Form settings
 */
pure Dict getFormSettings() =>
{
};



/**
 * Custom class name
 */
pure string customClassName(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	return crud_settings["crud_class_name"];
}



/**
 * Custom field render
 */
pure html calcField(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	return value;
}



/**
 * Custom field render
 */
pure html renderField(LayoutModel layout, string kind, string field_name, var value, Dict crud_settings)
{
	/* Table number */
	if (kind == "table" and field_name == "number")
		return static::renderNumber(layout, value, crud_settings);
	
	/* Href */
	if (kind == "table" and field_name == "layer_href")
	{
		Dict item = crud_settings["crud_item"];
		string uri = "http://" ~ item["domain_name"] ~ item["route"];
		return uri;
	}
	
	/* Open button */
	if (kind == "table" and field_name == "open")
	{
		Dict item = crud_settings["crud_item"];
		string uri = "http://" ~ item["domain_name"] ~ item["route"];
		return static::renderButton
		(
			layout, crud_settings, "view",
			{
				"url": uri,
				"target": "_blank",
				"label": "Open",
			}
		);
	}
	
	/* Table edit buttons */
	if (kind == "table" and field_name == "edit-buttons")
		return static::renderButtons(layout, value, crud_settings);
	
	return null;
}


</script>



</class>