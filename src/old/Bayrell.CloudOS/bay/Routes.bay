/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS;

use Runtime.LambdaChain;
use Runtime.LambdaChainClass;
use Runtime.LambdaChainDeclare;
use Runtime.ORM.MySQL.Settings as MySQLSettings;
use Runtime.Web.RenderDriver;
use Runtime.Web.App.LayoutModel;
use Runtime.Web.App.RenderContainer;
use Runtime.Web.App.Response;
use Runtime.Web.App.Route;
use Runtime.Web.App.RouteList;
use Runtime.Web.App.WebApp;
use Bayrell.CloudOS.DefaultLayout;
use Bayrell.CloudOS.Page404;
use Bayrell.CloudOS.Pattern;
use Bayrell.CloudOS.Page.AdminerPage;


@RouteList{}
@LambdaChainClass{}
static class Routes
{
	
	@LambdaChain
	{
		"name": RenderDriver::COMPONENTS_CHAIN,
	}
	pure list componentsChain(Collection components) => [ components ];
	
	
	#ifdef BACKEND then
	
	
	/**
	 * Render chain. Set default pattern
	 */
	@LambdaChain
	{
		"name": WebApp::RENDER_CHAIN,
		"pos": WebApp::RENDER_CHAIN_RESPONSE_BEFORE
	}
	pure RenderContainer renderChain_PatternDefault(RenderContainer container)
	{
		if (container.pattern_name == "default" and container.pattern_class == "" or container.pattern_class == "")
		{
			container <= pattern_class <= classof Pattern;
		}
		return [ container ];
	}
	
	
	
	/**
	 * Render chain. Route after
	 */
	@LambdaChain
	{
		"name": WebApp::RENDER_CHAIN,
		"pos": WebApp::RENDER_CHAIN_CALL_ROUTE_AFTER
	}
	pure RenderContainer renderChain_RouteAfter(RenderContainer container)
	{
		if (container.layout.layout_name == "admin")
		{
			/*
			Collection<Dict> items = TopMenu::select()
			|> .order([
				["pos", "desc"],
				["href", "asc"],
			])
			|> .asRecord(false)
			|> await .all()
			|> default Collection [];
			
			container <= layout <= keep_data <= cloud_os_top_menu <= items;
			*/
		}
		
		return [ container ];
	}
	
	#endif
	
	
	
	/**
	 * Render chain
	 * Page not found
	 * @return RenderContainer
	 */
	@LambdaChain
	{
		"name": WebApp::RENDER_CHAIN,
		"pos": WebApp::RENDER_CHAIN_CALL_PAGE_NOT_FOUND,
		"is_async": true
	}
	static async RenderContainer renderChain_Page404(RenderContainer container)
	{
		if (container == null or container.isResponseExists() or container.isPageExists()) return [ container ];
		
		/* Set title */
		container <= layout <= title <= "Page not found";
		container <= layout <= layout_name <= "default";
		
		/* Set page */
		container = container.setPage(classof Page404, null);
		
		/* Set 404 code */
		container <= new_http_code <= 404;
		
		return [ container ];
	}
	
	
	
	/**
	 * Layout chain
	 */
	@LambdaChain
	{
		"name": WebApp::RENDER_CHAIN,
		"pos": WebApp::RENDER_CHAIN_LAYOUT + 10,
	}
	pure LayoutModel renderChain_Layout(RenderContainer container)
	{
		if (container.layout.layout_name == "default") container <= layout <= layout_class <= classof DefaultLayout;
		if (container.layout.layout_name == "admin") container <= layout <= layout_class <= classof DefaultLayout;
		if (container.layout.layout_class == "") container <= layout <= layout_class <= classof DefaultLayout;
		return [ container ];
	}
	
	
	
	/**
	 * Title chain
	 */
	@LambdaChain
	{
		"name": WebApp::TITLE_CHAIN
	}
	pure RenderContainer titleChain(LayoutModel layout, string title)
	{
		title = title ~ " | Bayrell Cloud OS";
		return [ layout, title ];
	}
	
	
	
	#ifdef BACKEND then
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/database/", "name": "app.admin.database" }
	static async RenderContainer adminerPage(RenderContainer container)
	{
		MySQLSettings settings = @.config(["Runtime.ORM.MySQL", "default"]);
		
		/* Set title */
		container <= layout <= title <= "Admin database";
		
		/* Set page */
		container = container.setPage(classof AdminerPage, new Dict{ "db": settings.toDict() });
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/env/", "name": "app.admin.env" }
	static async RenderContainer adminerEnvPage(RenderContainer container)
	{
		string content = "";
		Dict env = @.enviroments;
		
		#ifcode PHP then
		@ob_start();
		echo "<pre>";
		var_dump($env);
		echo "</pre>";
		$content = ob_get_contents();
		@ob_end_clean();
		#endif
		
		container <= response <= new Response{ "content": content };
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/phpinfo/", "name": "app.admin.phpinfo" }
	static async RenderContainer adminerInfoPage(RenderContainer container)
	{
		string content = "";
		
		#ifcode PHP then
		@ob_start();
		phpinfo();
		$content = ob_get_contents();
		@ob_end_clean();
		#endif
		
		container <= response <= new Response{ "content": content };
		
		return [ container ];
	}
	
	
	
	/**
	 * Route Action
	 * @return WebContainer
	 */
	@Route{ "uri": "/database/{version}/", "name": "app.admin.database.version" }
	static async RenderContainer adminerVersionPage(RenderContainer container)
	{
		string content = "";
		string version = container.route_params.get("version", "");
		
		#ifcode PHP then
		$file_path = \Runtime\rtl::getModulePath($ctx, "Bayrell.CloudOS") . "/data/adminer-4.7.6.php";
		@ob_start();
		include $file_path;
		$content = ob_get_contents();
		@ob_end_clean();
		#endif
		
		container <= response <= new Response{ "content": content };
		
		return [ container ];
	}
	
	
	#endif
}