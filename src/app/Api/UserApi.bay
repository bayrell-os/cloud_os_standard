/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Api;

use Runtime.re;
use Runtime.DateTime;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.Relation;
use Runtime.Web.Crud.CrudPageApi;
use Runtime.Web.Crud.Rules.CrudRule;
use Runtime.Web.Crud.Rules.LowerCase;
use Runtime.Web.Crud.Rules.Purify;
use Runtime.Web.Crud.Rules.Required;
use Runtime.Web.Crud.Rules.SaveFields;
use Runtime.Web.Crud.Rules.Unique;
use Bayrell.CloudOS.Models.User;


class UserApi extends CrudPageApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "app.user";
	
	
	/**
	 * Returns model name
	 */
	pure string getModelName() => classof User;
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "users";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		if (api_name == "crud.search") return "actionSearch";
		if (api_name == "crud.search_one") return "actionSearchOne";
		if (api_name == "crud.item") return "actionItem";
		if (api_name == "crud.save") return "actionSave";
		if (api_name == "crud.delete") return "actionDelete";
		return "";
	}
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
		new SaveFields
		{
			"fields":
			[
				"login",
				"name",
				"banned",
			]
		},
		new Purify{ "name": "login" },
		new LowerCase{ "name": "login" },
		new Required{ "name": "login" },
		new Required{ "name": "name" },
		new Required{ "name": "banned" },
		new Unique{ "keys": ["login"] },
	];
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q)
	{
		int space_id = (int)this.post_data.get("space_id");
		if (space_id > 0)
		{
			q.reverseJoinKey("spaces_users_user_id");
			q.where("spaces_users.space_id", "=", space_id);
		}
		
		q.orderBy("t.id", "desc");
		
		return q;
	}
	
}