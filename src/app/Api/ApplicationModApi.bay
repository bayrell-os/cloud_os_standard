/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Api;

use Runtime.re;
use Runtime.ORM.Connection;
use Runtime.ORM.Query;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Relation;
use Runtime.Web.Crud.CrudPageApi;
use Runtime.Web.Crud.Rules.CrudRule;
use Runtime.Web.Crud.Rules.Dictionary;
use Runtime.Web.Crud.Rules.SaveFields;
use Bayrell.CloudOS.Models.Application;
use Bayrell.CloudOS.Models.Modificator;


class ApplicationModApi extends CrudPageApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "app.application.modificator";
	
	
	/**
	 * Returns model name
	 */
	pure string getModelName() => classof Dict;
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "app_modificators";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		if (api_name == "crud.search") return "actionSearch";
		if (api_name == "crud.search_one") return "actionSearchOne";
		if (api_name == "crud.item") return "actionItem";
		if (api_name == "crud.save") return "actionSave";
		if (api_name == "crud.delete") return "actionDelete";
		return "";
	}
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
		new SaveFields
		{
			"fields":
			[
				"modificator_id",
			]
		}
	];
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q)
	{
		if (
			this.action == 'actionSearch' or
			this.action == 'actionSearchOne'
		)
		{
			int application_id = (int)this.post_data["application_id"];
			q.where("app_modificators.app_id", "=", application_id);
		}
		
		q = q
			.innerJoinKey("app_modificators__modificator")
			
			.orderBy("modificators.priority", "asc")
			.orderBy("modificators.name", "asc")
			
			.addFields([
				"modificators.name as modificator_name",
				"modificators.priority as modificator_priority",
			])
		;
		
		return q;
	}
	
	
	/**
	 * Before update item event
	 */
	async void onSaveBefore(Relation item, Dict update_data)
	{
		parent::onSaveBefore(item, update_data);
		
		if (item.isNew())
		{
			int application_id = (int)this.post_data["application_id"];
			item.set("app_id", application_id);
		}
	}
	
	
	/**
	 * Returns database params
	 */
	Dict getDatabaseParams() => { "name": "default" };
	
}