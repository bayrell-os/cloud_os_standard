/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Api;

use Runtime.re;
use Runtime.DateTime;
use Runtime.Exceptions.ApiException;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Query;
use Runtime.ORM.Relation;
use Runtime.Web.Crud.CrudPageApi;
use Runtime.Web.Crud.Rules.CrudRule;
use Runtime.Web.Crud.Rules.LowerCase;
use Runtime.Web.Crud.Rules.Purify;
use Runtime.Web.Crud.Rules.Required;
use Runtime.Web.Crud.Rules.SaveFields;
use Runtime.Web.Crud.Rules.Unique;
use Bayrell.CloudOS.Models.Workspace;


class WorkspaceRoleApi extends CrudPageApi
{
	Relation space = null;
	
	
	/**
	 * Returns api name
	 */
	pure string getApiName() => "app.workspace.roles";
	
	
	/**
	 * Returns model name
	 */
	pure string getModelName() => classof Workspace;
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "spaces_roles";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		if (api_name == "crud.search") return "actionSearch";
		if (api_name == "crud.search_one") return "actionSearchOne";
		if (api_name == "crud.item") return "actionItem";
		if (api_name == "crud.save") return "actionSave";
		if (api_name == "crud.delete") return "actionDelete";
		return "";
	}
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
		new SaveFields
		{
			"fields":
			[
				"name",
			]
		},
		new Purify{ "name": "name" },
		new Required{ "name": "name" },
		new Unique{"keys": ["space_id", "name"]},
	];
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q)
	{
		q = q
			|> .orderBy("id", "asc")
		;
		return q;
	}
	
	
	/**
	 * Save init event
	 */	 
	async void onSaveInit()
	{
		await parent::onSaveInit();
		
		Connection conn = this.getDatabaseConnection();
			
		int space_id = this.post_data["space_id"];
		this.space = await Relation::findById("spaces", conn, space_id);
		if (this.space == null)
		{
			throw new ApiException("Space not found");
		}
	}
	
	
	/**
	 * Validate item
	 */
	async Dict validateItem(Dict update_data)
	{
		update_data = await parent::validateItem(update_data);
		
		if (update_data["name"][0] != "@")
		{
			update_data <= name <= "@" ~ update_data["name"];
		}
		
		return update_data;
	}
	
	
	/**
	 * Before save item
	 */
	async void onSaveBefore(Relation item, Dict update_data)
	{
		await parent::onSaveBefore(item, update_data);
		
		if (item.isNew())
		{
			item.set("space_id", this.space.get("id"));
		}
	}
	
}