/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Backend.ORM;

use Runtime.lib;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.BigIntType;
use Runtime.ORM.Annotations.BooleanType;
use Runtime.ORM.Annotations.ForeignKey;
use Runtime.ORM.Annotations.Index;
use Runtime.ORM.Annotations.JsonType;
use Runtime.ORM.Annotations.Primary;
use Runtime.ORM.Annotations.StringType;
use Runtime.ORM.Annotations.TextType;
use Runtime.ORM.Annotations.Unique;
use Runtime.ORM.Annotations.Virtual;
use Runtime.ORM.ConnectionInterface as Connection;
use Runtime.ORM.CursorInterface as Cursor;
use Runtime.ORM.QueryBuilder;
use Runtime.ORM.Record;
use Bayrell.CloudOS.Backend.ORM.Domain;


struct Space extends Record
{
	@AutoIncrement{}
	@BigIntType{}
	int space_id;
	
	@StringType{}
	string space_name;
	
	@StringType{ "nullable": true }
	string domain_name;
	
	
	/**
	 * Returns table name
	 */
	pure string table_name() => "spaces";
	
	
	
	/**
	 * Keys
	 */
	pure Collection keys() =>
	[
		new Primary
		{
			"keys": ["space_id"],
		},
		new Unique
		{
			"keys": ["domain_name"],
		},
		new ForeignKey
		{
			"name": "domain_name",
			"class_name": classof Domain,
			"keys":
			[
				{"src": "domain_name", "dest": "domain_name"},
			],
			"delete": Record::RESTRICT,
			"update": Record::CASCADE,
		},
	];
	
	
	
	/**
	 * Process item to database
	 */
	static async Dict toDatabase(Connection conn, Dict item)
	{
		item = await parent::toDatabase(conn, item);
		
		/* Check domain_name */
		if (item.has("domain_name"))
		{
			string domain_name = item["domain_name"];
			if (rtl::isEmpty(domain_name))
			{
				item <= domain_name <= null;
			}
			else
			{
				item <= domain_name <= rs::trim(domain_name);
			}
		}
		
		return item;
	}
	
}
