/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Backend.ORM;

use Runtime.Crypt.Password;
use Runtime.ORM.Annotations.AutoIncrement;
use Runtime.ORM.Annotations.BigIntType;
use Runtime.ORM.Annotations.BooleanType;
use Runtime.ORM.Annotations.ForeignKey;
use Runtime.ORM.Annotations.Index;
use Runtime.ORM.Annotations.JsonType;
use Runtime.ORM.Annotations.Primary;
use Runtime.ORM.Annotations.StringType;
use Runtime.ORM.Annotations.Unique;
use Runtime.ORM.Annotations.Virtual;
use Runtime.ORM.ConnectionInterface as Connection;
use Runtime.ORM.CursorInterface as Cursor;
use Runtime.ORM.Record;
use Bayrell.CloudOS.Backend.ORM.NginxFile;


struct User extends Record
{
	@AutoIncrement{}
	@BigIntType{}
	int user_id;
	
	@StringType{}
	string login;
	
	@StringType{}
	string name;
	
	@BooleanType{}
	bool banned;
	
	@BooleanType{}
	bool is_deleted;
	
	
	/**
	 * Returns table name
	 */
	pure string table_name() => "users";
	
	
	/**
	 * Keys
	 */
	pure Collection keys() =>
	[
		new Primary
		{
			"keys": ["user_id"],
		},
		new Unique
		{
			"keys": ["login"],
		},
	];
	
	
	
	/**
	 * Process item to database
	 */
	static async Dict toDatabase(Connection conn, Dict item)
	{
		item = await parent::toDatabase(conn, item);
		if (item.has("login")) item <= login <= rs::strtolower(item["login"]);
		return item;
	}
	
	
	
	/**
	 * Change password
	 */
	static async void changePassword(int user_id, string password, Dict db_settings = null)
	{
		if (password != "")
		{
			/* Connect to database */
			Connection conn = await static::connect(db_settings);
			
			/* Set up password */
			await conn.insert_or_update
			(
				"users_auth",
				{
					"user_id": user_id,
					"method": "password",
				},
				{
					"value": Password::createHash(password),
				},
				db_settings
			);
			
		}
	}
	
	
	
	/**
	 * Check password
	 */
	static async bool checkPassword(int user_id, string password, Dict db_settings = null)
	{
		if (password == "") return false;
		
		/* Connect to database */
		Connection conn = await static::connect(db_settings);
		Cursor cursor =	await conn.select
		(
			"users_auth",
			[
				["user_id", "=", user_id],
				["method", "=", "password"],
			],
			0, 1,
			db_settings
		);
		Dict row = await cursor.fetchOne();
		await cursor.close();
		
		if (row == null) return false;
		
		/* Check password hash */
		string password_hash = row["value"];
		if (Password::verify(password, password_hash)) return true;
		
		return false;
	}
	
	
	
	/**
	 * Create passwd file
	 */
	static async void nginxPasswdCreate(Dict db_settings = null)
	{
		/* Execute query */
		Cursor cursor = static::select("u")
			|> .fields([
				"u.*",
				"a.value as password",
			])
			|> .leftJoin("users_auth", "a", "u.user_id = a.user_id and a.method='password'")
			|> .filter([
				["u.banned", "=", 0],
				["u.is_deleted", "=", 0],
			])
			|> await .execute()
		;
		
		string htpasswd = "";
		while (true)
		{
			Dict row = await cursor.fetch();
			if (row == null)
			{
				break;
			}
			
			string banned = row["banned"];
			string login = row["login"];
			string password = row["password"];
			
			if (banned == "0" and not rtl::isEmpty(password) and not rtl::isEmpty(login))
			{
				htpasswd ~= login ~ ":" ~ password ~ "\n";
			}
		}
		
		await cursor.close();
		
		/* Update nginx file */
		NginxFile file = await NginxFile::save
		(
			{
				"name": "/inc/htpasswd.inc",
				"content": htpasswd,
				"timestamp": rtl::time(),
				"enable": true,
				"is_deleted": false,
			},
			db_settings
		);
	}
}
