/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Frontend.DockerServices;

use Runtime.BaseObject;
use Runtime.Callback;
use Runtime.Serializer;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseModel;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.WidgetResultModel;
use Runtime.Widget.Dialog.DialogModel;
use Runtime.Widget.Dialog.DialogModel;
use Runtime.Widget.Table.TableModel;
use Bayrell.CloudOS.Frontend.DockerServices.StopDialog;


class StopDialogModel extends BaseModel
{
	string widget_name = "stop_dialog";
	string component = classof StopDialog;
	WidgetResultModel result = new WidgetResultModel;
	DialogModel dialog = new DialogModel;
	Dict item = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(Map params = null)
	{
		parent(params);
		
		if (params)
		{
			if (params.has("table")) this.table = params.get("table");
		}
		
		/* Setup dialog */
		this.dialog.title = "Stop item";
		this.dialog.buttons.styles.add("align-end");
		
		/* Setup close buttons */
		ButtonModel close_button = this.dialog.buttons.add(new ButtonModel{
			"content": "Cancel",
			"styles": [
				"gray"
			],
		});
		close_button.events.add("onClick", new Callback(this, "onCloseButtonClick"));
		
		/* Setup confirm button */
		ButtonModel confirm_button = this.dialog.buttons.add(new ButtonModel{
			"content": "Stop",
			"styles": [
				"danger"
			],
		});
		confirm_button.events.add("onClick", new Callback(this, "onConfirmButtonClick"));
	}
	
	
	/**
	 * On click
	 */
	async void onClick(Dict data = null)
	{
		int row_number = -1;
		if (data != null and data.has("row_number"))
		{
			row_number = data.get("row_number");
		}
		
		if (row_number == -1) return;
		
		Dict item = this.parent_widget.getItemByRowNumber(row_number);
		this.item = Serializer::copy(item);
		
		/* Show dialog */
		this.dialog.show();
	}
	
	
	/**
	 * On close button click
	 */
	void onCloseButtonClick()
	{
		this.dialog.hide();
	}
	
	
	/**
	 * On confirm button click
	 */
	async void onConfirmButtonClick()
	{
		await this.stop();
		if (this.dialog.result.isSuccess())
		{
			this.dialog.hide();
			await this.parent_widget.refresh();
		}
	}
	
	
	/**
	 * Stop service
	 */
	async void stop()
	{
		this.dialog.result.setWaitMessage();
		ApiResult res = await this.layout.callApi({
			"api_name": "app.docker.services::crud",
			"method": "actionStop",
			"data": {
				"pk": {
					"service_id": this.item.get("service_id"),
				},
			}
		});
		
		this.dialog.result.setApiResult(res);
	}
}
