/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Frontend.Applications;

use Runtime.Callback;
use Runtime.Entity.Factory;
use Runtime.Web.BasePageModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.RenderListModel;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.SelectModel;
use Runtime.Widget.Form.FormDeleteStorage;
use Runtime.Widget.Form.FormMessage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Form.FormSubmitStorage;
use Runtime.Widget.Tab.TabsModel;
use Runtime.Widget.Table.TableDialogModel;


class ModificatorsTable extends RenderListModel
{
	int app_id = 0;
	FormModel form = null;
	TableDialogModel table = null;
	RowButtonsModel top_buttons = null;
	SelectModel modificators = null;
	
	
	/**
	 * Init widget params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("app_id")) this.app_id = params.get("app_id");
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		Dict foreign_key =
		{
			"app_id": this.app_id,
		};
		
		/* Add modificators */
		this.modificators = this.addWidget(
			classof SelectModel,
			{
				"widget_name": "modificators",
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "app.modificators::search",
					}
				),
				"transform": Map (Dict item) => {
					"key": item.get("id"),
					"value": item.get("name") ~ " <" ~ item.get("uid") ~ ">",
				},
			}
		);
		
		/* Add form */
		this.form = this.addWidget(
			classof FormModel,
			{
				"widget_name": "form",
				"storage": new Factory
				(
					"Runtime.Widget.Form.FormSaveStorage",
					{
						"api_name": "app.applications.modificators::save",
					}
				),
				"primary_key": ["app_id", "modificator_id"],
				"foreign_key": foreign_key,
				"fields":
				[
					{
						"name": "modificator_id",
						"label": "Name",
						"model": this.modificators,
					},
				],
				"events":
				{
					"submit": new Callback(this, "onFormSubmit"),
				}
			}
		);
		
		/* Add table */
		this.table = this.addWidget(
			classof TableDialogModel,
			{
				"widget_name": "table",
				"get_title": void (Dict params)
				{
					string action = params.get("action");
					Dict item = params.get("item");
					if (action == "add")
					{
						return "Add modificator";
					}
					if (action == "edit")
					{
						return "Edit modificator '" ~ item.get("name") ~ "'";
					}
					if (action == "delete")
					{
						return "Delete modificator '" ~ item.get("name") ~ "'";
					}
					return "";
				},
				"styles": ["border"],
				
				/* Table storage */
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "app.applications.modificators::search",
					}
				),
				
				/* Foreign key */
				"foreign_key": foreign_key,
				
				/* Form */
				"add_form": this.form,
				"edit_form": this.form,
				
				/* Delete form */
				"delete_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "delete_form",
						"primary_key": ["app_id", "modificator_id"],
						"storage": new Factory(
							classof FormDeleteStorage,
							{
								"api_name": "app.applications.modificators::save"
							}
						),
						"events":
						{
							"submit": new Callback(this, "onFormSubmit"),
						}
					}
				),
				
				/* Table fields */
				"fields":
				[
					{
						"name": "row_number",
					},
					{
						"name": "name",
						"label": "Name",
					},
					{
						"name": "priority",
						"label": "Priority",
					},
					{
						"name": "row_buttons",
						"model": new ModelFactory(
							"Runtime.Widget.Table.TableRowButtonsModel",
							{
								"edit": false,
							}
						),
					},
				]
			}
		);
		
		/* Add top buttons */
		this.top_buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "top_buttons",
				"styles": ["top_buttons"],
				"buttons":
				[
					new ModelFactory
					(
						"Runtime.Widget.Table.AddButtonModel",
						{
							"table": this.table,
						}
					)
				],
			}
		);
		
		/* Add render list */
		this.addItem(this.top_buttons);
		this.addItem(this.table);
	}
	
	
	/**
	 * Form submit
	 */
	async void onFormSubmit(FormMessage message)
	{
		if (not message.result.isSuccess()) return;
		await this.parent_widget.form.submit();
		await this.parent_widget.form.loadForm();
	}
}