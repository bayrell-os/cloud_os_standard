/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Frontend.NginxFiles;

use Runtime.Callback;
use Runtime.Entity.Factory;
use Runtime.Web.BasePageModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.Form.FormDeleteStorage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Form.FormSubmitStorage;
use Runtime.Widget.Table.TableDialogModel;
use Bayrell.CloudOS.Frontend.NginxFiles.NginxPage;


class NginxPageModel extends BasePageModel
{
	string component = classof NginxPage;
	FormModel form = null;
	TableDialogModel table = null;
	RowButtonsModel top_buttons = null;
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Add top buttons */
		this.top_buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "top_buttons",
				"buttons":
				[
					{
						"content": "Add",
						"widget_name": "add_button",
						"styles": [
							"success",
						]
					},
				],
				"events":
				{
					"click": new Callback(this, "onTopButtonsClick"),
				},
			}
		);
		
		/* Add form */
		this.form = this.addWidget(
			classof FormModel,
			{
				"widget_name": "edit_form",
				"primary_key": ["id"],
				"storage": new Factory
				(
					"Runtime.Widget.Form.FormSaveStorage",
					{
						"api_name": "app.nginx_files::save",
					}
				),
				"fields":
				[
					{
						"name": "name",
						"label": "File name",
						"component": "Runtime.Widget.Input",
					},
					{
						"name": "enable",
						"label": "Enable",
						"component": "Runtime.Widget.Select",
						"props": {
							"options": [
								{"key": "0", "value": "No"},
								{"key": "1", "value": "Yes"},
							],
						},
					},
					{
						"name": "content",
						"label": "Content",
						"component": "Runtime.Widget.CodeMirror.CodeMirror",
						"props": {
							"height": "450px",
						},
					},
				],
			}
		);
		
		/* Add table */
		this.table = this.addWidget(
			classof TableDialogModel,
			{
				"widget_name": "table",
				"get_title": void (Dict params)
				{
					string action = params.get("action");
					Dict item = params.get("item");
					if (action == "add")
					{
						return "Add nginx file";
					}
					if (action == "edit")
					{
						return "Edit nginx file '" ~ item.get("name") ~ "'";
					}
					if (action == "delete")
					{
						return "Delete nginx file '" ~ item.get("name") ~ "'";
					}
					return "";
				},
				"styles": ["border"],
				
				/* Table storage */
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "app.nginx_files::search",
					}
				),
				
				/* Set page */
				"page": this.layout.request_query.get("page", 1) - 1,
				
				/* Form */
				"add_form": this.form,
				"edit_form": this.form,
				
				/* Delete form */
				"delete_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "delete_form",
						"primary_key": ["id"],
						"storage": new Factory(
							classof FormDeleteStorage,
							{
								"api_name": "app.nginx_files::save"
							}
						),
					}
				),
				
				/* Table fields */
				"fields":
				[
					{
						"name": "row_number",
					},
					{
						"name": "name",
						"label": "File name",
					},
					{
						"name": "enable",
						"label": "Enable",
						"component": "Runtime.Widget.SelectLabel",
						"props": {
							"options": [
								{"key": "0", "value": "No"},
								{"key": "1", "value": "Yes"},
							],
						},
					},
					{
						"name": "row_buttons",
						"model": new ModelFactory("Runtime.Widget.Table.TableRowButtonsModel"),
					},
				],
			}
		);
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
		this.layout.setPageTitle("List of nginx files");
	}
	
	
	/**
	 * Top buttons click
	 */
	void onTopButtonsClick(ClickMessage message)
	{
		if (message.widget.widget_name == "add_button")
		{
			this.table.save_dialog.showAdd();
		}
	}
}