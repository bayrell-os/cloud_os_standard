/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Frontend.Workspace;

use Runtime.Web.PageModel;
use Runtime.Web.RenderContainer;
use Runtime.Widget.RenderListModel;
use Runtime.Widget.Crud.CrudPageModel;
use Bayrell.CloudOS.Frontend.Workspace.DomainTableModel;
use Bayrell.CloudOS.Frontend.Workspace.WorkspaceFormModel;
use Bayrell.CloudOS.Frontend.Workspace.WorkspacePageModel;
use Bayrell.CloudOS.Frontend.Workspace.WorkspaceTableModel;


class WorkspacePageModel extends CrudPageModel
{
	/**
	 * Init widget settings
	 */
	void initWidget()
	{
		parent();
		
		if (this.layout.route.name == "app:workspace:index")
		{
			/* Set action */
			this.setAction("index");
			
			/* Create table widget */
			WorkspaceTableModel table = this.addWidget(classof WorkspaceTableModel);
			this.render_list.addItem(table);
		}
		
		else if (this.layout.route.name == "app:workspace:edit")
		{
			/* Set action */
			this.setAction("edit");
			
			/* Create form widget */
			WorkspaceFormModel form = this.addWidget(
				classof WorkspaceFormModel,
				{
					"action": "edit",
				}
			);
			
			/* Set primary key for id */
			form.storage.setPrimaryKey({
				"id": this.layout.route.matches.get("id"),
			});
			
			/* Add form to render list */
			this.render_list.addItem(form);
			
			/* Create domains table widget */
			DomainTableModel domain_table = this.addWidget(
				classof DomainTableModel,
				{
					"widget_name": "domain_table",
					"title": "Domains",
				}
			);
			domain_table.storage.setPage(1);
			
			/* Add table to render list */
			this.render_list.addItem(domain_table);
			
			/* Set foreign key */
			int id = this.layout.route.matches.get("id");
			domain_table.storage.setForeignKey("space_id", id);
			domain_table.getWidget("form").storage.setForeignKey("space_id", id);
		}
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
		/* Set title */
		if (this.action == "index")
		{
			this.layout.setPageTitle("List of workspaces");
		}
		else if (this.action == "edit")
		{
			WorkspaceFormModel form = this.getWidget("form");
			this.layout.setPageTitle(form.getPageTitle("edit"));
		}
	}
}