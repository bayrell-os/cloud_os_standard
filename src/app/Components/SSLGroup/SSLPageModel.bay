/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.SSLGroup;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Widget.Crud.CrudPageModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldItem;
use Runtime.Widget.Input;
use Runtime.Widget.Label;
use Runtime.Widget.TextArea;


struct SSLPageModel extends CrudPageModel
{
	/**
	 * Fields
	 */
	pure static Dict fields() =>
	{
		"name": new FieldInfo{
			"label": "name",
			"name": "name",
		},
		"container_name": new FieldInfo{
			"label": "Container name",
			"name": "container_name",
		},
	};
	
	
	/**
	 * Init components
	 */
	async static void initComponents(ModelProxy<BaseModel> model, BaseRoute route)
	{
		route.layout.commit("addComponent", classof Input);
		route.layout.commit("addComponent", classof Label);
		route.layout.commit("addComponent", classof TextArea);
	}
	
	
	/**
	 * Init widget table
	 */
	async static void initWidgetTable
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.ssl_group");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Set href */
		widget.commit("setGetHref", new Callback(static::getClassName(), "getHref"));
		
		/* Set table fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "row_number"},
			new FieldItem{"name": "name", "component": classof Label},
			new FieldItem{"name": "container_name", "component": classof Label},
			new FieldItem{"name": "row_buttons"},
		]);
		
		/* Setup page */
		widget.commit("setPage", route.container.request.query.get("page", 1));
	}
	
	
	/**
	 * Init widget form
	 */
	async static void initWidgetForm
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.ssl_group");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Set form fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "name", "component": classof Input},
			new FieldItem{"name": "container_name", "component": classof Input},
		]);
		
		/* Setup primary key */
		if (
			widget.data("widget_path") == "form" and
			route.container.route.name != "app:ssl:add"
		)
		{
			widget.commit("setPrimaryKey", {
				"id": route.container.route.matches.get("id"),
			});
		}
		
		/* Setup back page */
		route.page_model.commit("setBackPage", static::url("app:ssl:index"));
	}
	
	
	/**
	 * Returns href
	 */
	static string getHref(string name, Dict params = null)
	{
		if (name == "add")
		{
			return static::url("app:ssl:add");
		}
		else if (name == "edit")
		{
			return static::url("app:ssl:edit", {"id": params["item", "id"]});
		}
		else if (name == "delete")
		{
			return static::url("app:ssl:delete", {"id": params["item", "id"]});
		}
		return "";
	}
	
	
	/**
	 * Get names
	 */
	static string getNames(string action, Dict obj = null)
	{
		if (action == "name")
		{
			return "SSL group";
		}
		if (action == "names")
		{
			return "SSL groups";
		}
		else if (action == "item_name")
		{
			return obj["item", "name"];
		}
	}
	
}