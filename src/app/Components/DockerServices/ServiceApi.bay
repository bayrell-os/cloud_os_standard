/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.DockerServices;

use Runtime.re;
use Runtime.DateTime;
use Runtime.Exceptions.ApiException;
use Runtime.ORM.Query;
use Runtime.ORM.Relation;
use Runtime.Widget.Crud.Api.CrudApi;
use Runtime.Widget.Crud.Api.CrudRule;
use Runtime.Widget.Crud.Api.LowerCase;
use Runtime.Widget.Crud.Api.Purify;
use Runtime.Widget.Crud.Api.Required;
use Runtime.Widget.Crud.Api.Unique;
use Bayrell.CloudOS.Lib.DockerService;


class ServiceApi extends CrudApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "app.docker.services";
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "docker_services";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		if (api_name == "crud.search") return "actionSearch";
		if (api_name == "crud.search_one") return "actionSearchOne";
		if (api_name == "crud.item") return "actionItem";
		if (api_name == "service_stop") return "actionServiceStop";
		return "";
	}
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() => [
		"service_id",
		"stack_name",
		"service_name",
		"docker_image",
		"docker_tasks",
		"count_work",
		"count_total",
		"gmtime_created",
		"gmtime_updated",
	];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() => [
	];
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
	];
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q)
	{
        q.where("is_deleted", "=", 0);
        q.where("enable", "=", 1);
		q.orderBy("stack_name", "asc");
		return q;
	}
	
	
	/**
	 * Action service stop
	 */
	async void actionServiceStop()
	{
		/* Init api */
		await this.initApi("actionCompose");
		if (this.container.response.error_name != "") return;
		
		/* Find item */
		Dict pk = this.post_data["pk"];
		this.item = await this.findItem(pk);
		
		/* Item not found */
		if (this.item == null)
		{
			this.container.fail( new ApiException("Item not found") );
			return;
		}
		
		/* Stop service */
		string service_name = this.item.get("stack_name") ~ "_" ~ this.item.get("service_name");
		string message = DockerService::stopService(service_name);
		Dict item_pk = this.item.getPrimaryKey();
		
		/* Set is delete */
		this.item.set("enable", 0);
		this.item.set("is_deleted", 1);
		this.item.save();
		
		/* Set api result */
		this.container.success({
			"message": message,
			"data":
			{
				"pk": item_pk,
			}
		});
	}
	
}