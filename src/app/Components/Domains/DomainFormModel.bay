/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Domains;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Widget.Crud.CrudDataStorage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldList;
use Runtime.Widget.Input;
use Bayrell.CloudOS.Components.Domains.DomainPageModel;
use Bayrell.CloudOS.Components.Routes.RouteFormModel;
use Bayrell.CloudOS.Components.Routes.RoutePageModel;
use Bayrell.CloudOS.Components.Routes.RouteTableModel;


struct DomainFormModel extends FormModel
{
	/**
	 * Fields
	 */
	pure static Dict fields() =>
	{
		"domain_name": new FieldInfo{
			"label": "Domain name",
			"name": "domain_name",
			"component": classof Input,
		}
	};
	
	
	/**
	 * Init widget
	 */
	async static void initWidget(ModelProxy<BaseModel> model, ModelProxy<Layout> layout)
	{
		await parent::initWidget(model, layout);
		
		/* Get storage */
		ModelProxy<CrudDataStorage> data = model.proxy("data");
		ModelProxy<FieldList> fields = model.proxy("fields");
		
		/* Api name */
		data.commit("setApiName", "app.domain");
		
		/* Set fields */
		fields.commit("setFields", static::fields());
		fields.commit("setPrimaryKeys", ["id"]);
		
		/* Set fields list */
		fields.commit("setFieldsList", [
			"row_number",
			"domain_name",
			"row_buttons",
		]);
		
		/* Set get names */
		model.commit("setGetNames", new Callback(classof DomainPageModel, "getNames"));
		
		/* Add components */
		layout.commit("addComponent", classof Input);
	}
	
	
	/**
	 * Init sub widgets
	 */
	static void initSubWidgets(ModelProxy<BaseModel> model, ModelProxy<Layout> layout)
	{
		/* Add routes */
		await model.commitAsync("addForeignWidget", layout, {
			"widget_name": "routes",
			"form_model": method RoutePageModel::createFormModel,
			"table_model_name": classof TableDialogModel,
			"table_model_init": method RouteTableModel::initWidget,
			"foreign_key": "domain_id",
		});
		
		/* Setup table routes */
		ModelProxy<FormModel> routes_table = model.proxy(["widgets", "routes", "widgets", "table"]);
		if (routes_table.data() != null)
		{
			/* Set width */
			routes_table.commit("setDialogWidth", "save", "800px");
		}
	}
	
}