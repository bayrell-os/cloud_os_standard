/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Domains;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Widget.Crud.CrudPageModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldItem;
use Runtime.Widget.Input;
use Runtime.Widget.Label;
use Runtime.Widget.SelectLabel;
use Runtime.Widget.TextArea;
use Bayrell.CloudOS.Components.Routes.RoutePageModel;


struct DomainPageModel extends CrudPageModel
{
	/**
	 * Fields
	 */
	pure static Dict fields() =>
	{
		"domain_name": new FieldInfo{
			"label": "Domain name",
			"name": "domain_name",
		},
	};
	
	
	/**
	 * Init components
	 */
	async static void initComponents(ModelProxy<BaseModel> model, BaseRoute route)
	{
		route.layout.commit("addComponent", classof Input);
		route.layout.commit("addComponent", classof Label);
		route.layout.commit("addComponent", classof SelectLabel);
	}
	
	
	/**
	 * Init widget table
	 */
	async static void initWidgetTable
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.domain");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Setup get href */
		widget.commit("setGetHref", new Callback(static::getClassName(), "getHref"));
		
		/* Set table fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "row_number"},
			new FieldItem{"name": "domain_name", "component": classof Label},
			new FieldItem{"name": "row_buttons"},
		]);
		
		/* Setup page */
		widget.commit("setPage", route.container.request.query.get("page", 1));
	}
	
	
	/**
	 * Init widget form
	 */
	async static void initWidgetForm
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.domain");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Set form fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "domain_name", "component": classof Input},
		]);
		
		/* Setup primary key */
		if (
			widget.data("widget_path") == "form" and
			route.container.route.name != "app:domains:add"
		)
		{
			widget.commit("setPrimaryKey", {
				"id": route.container.route.matches.get("id"),
			});
		}
		
		/* Setup back page */
		page_model.commit("setBackPage", static::url("app:domains:index"));
		
		/* Add widget routes */
		if (
			page_model.data("action") == "save" and
			not widget.call("isNew")
		)
		{
			TableDialogModel routes = await route.addWidget(
				new TableDialogModel{ "widget_name": "routes" }
			);
			await static::initWidgetRoutes(page_model, routes, route);
		}
	}
	
	
	/**
	 * Setup widget routes
	 */
	async static void initWidgetRoutes
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Add components */
		await RoutePageModel::initComponents(page_model, route);
		
		/* Init table */
		await RoutePageModel::initWidgetTable(page_model, widget, route);
		
		/* Init form */
		ModelProxy<BaseModel> form = widget.proxy(["widgets", "form"]);
		await RoutePageModel::initWidgetForm(page_model, form, route);
		
		/* Set foreign key */
		int domain_id = (int)page_model.data(["widgets", "form", "item_pk", "id"]);
		widget.commit("setForeignKey", "domain_id", domain_id);
		form.commit("setForeignKey", "domain_id", domain_id);
		
		/* Remove domain_id */
		form.commit("removeFieldFromList", "domain_id");
		
		/* Set width */
		widget.commit("setDialogWidth", "save", "800px");
	}
	
	
	/**
	 * Returns href
	 */
	static string getHref(string name, Dict params = null)
	{
		if (name == "add")
		{
			return static::url("app:domains:add");
		}
		else if (name == "edit")
		{
			return static::url("app:domains:edit", {"id": params["item", "id"]});
		}
		else if (name == "delete")
		{
			return static::url("app:domains:delete", {"id": params["item", "id"]});
		}
		return "";
	}
	
	
	/**
	 * Get names
	 */
	static string getNames(string action, Dict obj = null)
	{
		if (action == "name")
		{
			return "domain";
		}
		if (action == "names")
		{
			return "domains";
		}
		else if (action == "item_name")
		{
			return obj["item", "domain_name"];
		}
	}
	
}