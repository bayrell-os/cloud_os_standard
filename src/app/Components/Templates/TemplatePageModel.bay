/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Templates;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Widget.Crud.CrudPageModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldItem;
use Runtime.Widget.Input;
use Runtime.Widget.Label;
use Runtime.Widget.Select;
use Runtime.Widget.SelectLabel;
use Runtime.Widget.TextArea;
use Bayrell.CloudOS.Components.Templates.VersionPageModel;


struct TemplatePageModel extends CrudPageModel
{
	/**
	 * Fields
	 */
	pure static Dict fields() =>
	{
		"uid": new FieldInfo{
			"label": "UID",
			"field_name": "uid",
		}
		"name": new FieldInfo{
			"label": "Name",
			"field_name": "name",
		},
		"content": new FieldInfo{
			"label": "Content",
			"name": "content",
		},
	};
	
	
	/**
	 * Init components
	 */
	async static void initComponents(ModelProxy<BaseModel> model, BaseRoute route)
	{
		route.layout.commit("addComponent", classof Input);
		route.layout.commit("addComponent", classof Label);
		route.layout.commit("addComponent", classof Select);
		route.layout.commit("addComponent", classof SelectLabel);
		route.layout.commit("addComponent", classof TextArea);
	}
	
	
	/**
	 * Setup widget routes
	 */
	async static void setupWidgetTable
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.template");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Setup get href */
		widget.commit("setGetHref", new Callback(static::getClassName(), "getHref"));
		
		/* Set table fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "row_number"},
			new FieldItem{"name": "name", "component": classof Label},
			new FieldItem{"name": "uid", "component": classof Label},
			new FieldItem{"name": "row_buttons"},
		]);
		
		/* Setup page */
		widget.commit("setPage", route.container.request.query.get("page", 1));
	}
	
	
	/**
	 * Setup widget routes
	 */
	async static void setupWidgetForm
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		/* Api name */
		widget.commit("setApiName", "app.template");
		
		/* Set fields */
		widget.commit("setFields", static::fields());
		
		/* Set form fields list */
		widget.commit("setFieldsList", [
			new FieldItem{"name": "name", "component": classof Input},
			new FieldItem{"name": "uid", "component": classof Input},
		]);
		
		/* Setup primary key */
		if (route.container.route.name != "app:templates:add")
		{
			widget.commit("setPrimaryKey", {
				"id": route.container.route.matches.get("id"),
			});
		}
		
		/* Setup back page */
		route.page_model.commit("setBackPage", static::url("app:templates:index"));
		
		/* Add widget versions */
		if (
			page_model.data("action") == "save" and
			not widget.call("isNew")
		)
		{
			TableDialogModel versions = await route.addWidget(
				new TableDialogModel{ "widget_name": "versions" }
			);
			await static::initWidgetVersions(page_model, versions, route);
		}
	}
	
	
	/**
	 * Setup widget routes
	 */
	async static void initWidgetVersions
	(
		ModelProxy<BaseModel> page_model,
		ModelProxy<BaseModel> widget,
		BaseRoute route
	)
	{
		widget.commit("showTableTop", false);
		
		/* Add components */
		await VersionPageModel::initComponents(page_model, route);
		
		/* Init table */
		await VersionPageModel::initWidgetTable(page_model, widget, route);
		
		/* Init form */
		ModelProxy<BaseModel> form = widget.proxy(["widgets", "form"]);
		await VersionPageModel::initWidgetForm(page_model, form, route);
		
		/* Set foreign key */
		int template_id = (int)page_model.data(["widgets", "form", "item_pk", "id"]);
		widget.commit("setForeignKey", "template_id", template_id);
		form.commit("setForeignKey", "template_id", template_id);
		
		/* Remove template_id */
		form.commit("removeFieldFromList", "template_id");
		
		/* Set width */
		widget.commit("setDialogWidth", "save", "800px");
	}
	
	
	/**
	 * Returns href
	 */
	static string getHref(string name, Dict params = null)
	{
		if (name == "add")
		{
			return static::url("app:templates:import");
		}
		else if (name == "edit")
		{
			return static::url("app:templates:edit", {"id": params["item", "id"]});
		}
		else if (name == "delete")
		{
			return static::url("app:templates:delete", {"id": params["item", "id"]});
		}
		return "";
	}
	
	
	/**
	 * Get names
	 */
	static string getNames(string action, Dict obj = null)
	{
		if (action == "name")
		{
			return "template";
		}
		if (action == "names")
		{
			return "templates";
		}
		else if (action == "item_name")
		{
			return obj["item", "name"] ~ "<" ~ obj["item", "uid"]  ~ ">";
		}
	}
	
}