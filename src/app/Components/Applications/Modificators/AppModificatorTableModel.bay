/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Applications.Modificators;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Web.Events.MouseClickEvent;
use Runtime.Widget.Button;
use Runtime.Widget.Crud.CrudDataStorage;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldList;
use Runtime.Widget.Label;
use Bayrell.CloudOS.Components.Modificators.ModificatorPageModel;


struct AppModificatorTableModel extends TableDialogModel
{
	/**
	 * Fields
	 */
	pure static Dict fields() =>
	{
		"row_number": new FieldInfo{
			"name": "row_number",
		},
		"modificator_uid": new FieldInfo{
			"label": "UID",
			"name": "modificator_uid",
			"component": classof Label,
		},
		"modificator_name": new FieldInfo{
			"label": "name",
			"name": "modificator_name",
			"component": classof Label,
		},
		"modificator_version": new FieldInfo{
			"label": "Version",
			"name": "modificator_version",
			"component": classof Label,
		},
		"modificator_priority": new FieldInfo{
			"label": "Priority",
			"name": "modificator_priority",
			"component": classof Label,
		},
		"row_buttons": new FieldInfo{
			"name": "row_buttons",
		},
	};
	
	
	/**
	 * Init widget
	 */
	async static void initWidget(ModelProxy<BaseModel> model, ModelProxy<Layout> layout)
	{
		await parent::initWidget(model, layout);
		
		/* Get storage */
		ModelProxy<CrudDataStorage> data = model.proxy("data");
		ModelProxy<FieldList> fields = model.proxy("fields");
		
		/* Api name */
		data.commit("setApiName", "app.application.modificator");
		
		/* Set fields */
		fields.commit("setFields", static::fields());
		fields.commit("setPrimaryKeys", ["id"]);
		
		/* Set fields list */
		fields.commit("setFieldsList", [
			"row_number",
			"modificator_name",
			"modificator_priority",
			"row_buttons",
		]);
		
		/* Row buttons */
		fields.commit("editField", new FieldInfo{
			"name": "row_buttons",
			"render_method": new Callback{
				"obj": static::getClassName(), "name": "renderRowButtons"
			},
		});
		
		/* Set get route name */
		model.commit("setGetRouteName", new Callback(classof ModificatorPageModel, "getRouteName"));
		
		/* Add components */
		layout.commit("addComponent", classof Label);
	}
	
	
	/**
	 * Render row buttons
	 */
	static html renderRowButtons(int row_number, FieldInfo field, Dict item)
	{
		<div class="web_table__td_row_buttons">
			<Button type="danger small"
				@event:MouseClickEvent="onDeleteClick" data-item={{ item }}>Delete</Button>
		</div>
	}
	
}