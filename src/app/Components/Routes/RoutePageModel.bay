/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Routes;

use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.BaseModel;
use Runtime.Web.BaseRoute;
use Runtime.Web.Layout;
use Runtime.Web.RouteInfo;
use Runtime.Widget.Crud.CrudDataStorage;
use Runtime.Widget.Crud.CrudPageModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldList;
use Runtime.Widget.Input;
use Runtime.Widget.Label;
use Runtime.Widget.Select;
use Runtime.Widget.SelectLabel;
use Runtime.Widget.TextArea;


struct RoutePageModel extends CrudPageModel
{
	/**
	 * Fields
	 */
	pure static Dict fields(string type) =>
	{
		"row_number": new FieldInfo{
			"name": "row_number",
		},
		
		"enable": new FieldInfo{
			"label": "Enable",
			"name": "enable",
			"component": type == "form" ? classof Select : classof SelectLabel,
			"options": [
				{"label": "No", "value": "0"},
				{"label": "Yes", "value": "1"},
			],
		},
		"protocol": new FieldInfo{
			"label": "Protocol",
			"name": "protocol",
			"component": type == "form" ? classof Select : classof SelectLabel,
			"options": [
				{"label": "HTTP", "value": "http"},
			],
		},
		
		/* Source */
		"domain_id": new FieldInfo{
			"label": "Domain name",
			"name": "domain_id",
			"component": type == "form" ? classof Select : classof SelectLabel,
			"options": [
			],
		},
		"source_port": new FieldInfo{
			"label": "Source port",
			"name": "source_port",
			"component": type == "form" ? classof Input : classof Label,
		},
		"route_prefix": new FieldInfo{
			"label": "Route prefix",
			"name": "route_prefix",
			"component": type == "form" ? classof Input : classof Label,
		},
		
		/* Dest */
		"docker_name": new FieldInfo{
			"label": "Docker name",
			"name": "docker_name",
			"component": type == "form" ? classof Input : classof Label,
		},
		"target_port": new FieldInfo{
			"label": "Target port",
			"name": "target_port",
			"component": type == "form" ? classof Input : classof Label,
		},
		"target_prefix": new FieldInfo{
			"label": "Target prefix",
			"name": "target_prefix",
			"component": type == "form" ? classof Input : classof Label,
		},
		
		/* Nginx config */
		"nginx_config": new FieldInfo{
			"label": "Nginx config",
			"name": "nginx_config",
			"component": type == "form" ? classof TextArea : classof Label,
		},
		
		"source": new FieldInfo{
			"label": "Source",
			"name": "source",
			"value_method": new Callback(static::getClassName(), "getTableValue"),
		},
		"dest": new FieldInfo{
			"label": "Dest",
			"name": "dest",
			"value_method": new Callback(static::getClassName(), "getTableValue"),
		},
		"row_buttons": new FieldInfo{
			"name": "row_buttons",
		},
	};
	
	
	/**
	 * Primary keys
	 */
	pure static Collection getPrimaryKeys() => ["id"];
	
	
	/**
	 * Api name
	 */
	pure static Dict getApiName(string type) => "app.route";
	
	
	/**
	 * Init widget
	 */
	async static void initWidget(ModelProxy<BaseModel> model, ModelProxy<Layout> layout)
	{
		await parent::initWidget(model, layout);
	}
	
	
	/**
	 * Init table
	 */
	async static void initTableWidget(
		ModelProxy<CrudPageModel> model,
		ModelProxy<TableModel> table,
		ModelProxy<Layout> layout
	)
	{
		await parent::initTableWidget(model, table, layout);
		
		/* Set fields list */
		ModelProxy<FieldList> fields = table.proxy("fields");
		fields.commit("setFieldsList", [
			"row_number",
			"enable",
			"protocol",
			"source",
			"dest",
			"row_buttons",
		]);
		
		/* Add components */
		layout.commit("addComponent", classof Label);
		layout.commit("addComponent", classof SelectLabel);
	}
	
	
	/**
	 * Init form
	 */
	async static void initFormWidget(
		ModelProxy<CrudPageModel> model,
		ModelProxy<FormModel> form,
		ModelProxy<Layout> layout
	)
	{
		await parent::initFormWidget(model, form, layout);
		
		/* Set fields list */
		ModelProxy<FieldList> fields = form.proxy("fields");
		fields.commit("setFieldsList", [
			"enable",
			"protocol",
			"domain_id",
			"source_port",
			"route_prefix",
			"docker_name",
			"target_port",
			"target_prefix",
			"nginx_config",
		]);
		
		/* Add domain dictionary */
		fields.commit("addDictionaryField", {
			"from": "domains",
			"to": "domain_id",
			"option": new Callback(static::getClassName(), "getDomainOption"),
		});
		
		/* Set default item */
		ModelProxy<CrudDataStorage> data = form.proxy("data");
		data.commit("setDefaultItem", {
			"enable": 1,
			"protocol": "http",
			"source_port": "80",
			"route_prefix": "/",
			"target_port": "80",
			"target_prefix": "/",
		});
		
		/* Add components */
		layout.commit("addComponent", classof Input);
		layout.commit("addComponent", classof Select);
		layout.commit("addComponent", classof TextArea);
	}
	
	
	/**
	 * Dictionary domain
	 */
	static Dict getDomainOption(ModelProxy<FieldList> model, Dict item) =>
	{
		"label": item["domain_name"],
		"value": item["id"]
	};
	
	
	/**
	 * Returns route href
	 */
	string getRouteHref(string name, Dict obj = null)
	{
		if (name == "index")
		{
			return static::url("app:routes:index");
		}
		if (name == "add")
		{
			return static::url("app:routes:add");
		}
		if (name == "edit")
		{
			return static::url("app:routes:edit", {"id": obj["item", "id"]});
		}
		if (name == "delete")
		{
			return static::url("app:routes:delete", {"id": obj["item", "id"]});
		}
		return "";
	}
	
	
	/**
	 * Get names
	 */
	string getPageTitle(string action)
	{
		if (action == "name")
		{
			return "route";
		}
		if (action == "names")
		{
			return "routes";
		}
		else if (action == "item_name")
		{
			Dict item = this.getWidget("form").data.item;
			return static::getSourceColumn(item);
		}
		else if (action == "delete_content")
		{
			Dict item = this.getWidget("form").data.item;
			return "Delete route '" ~
				static::getSourceColumn(item) ~ "' => " ~
				static::getDestColumn(item) ~ " ?";
		}
		return parent::getPageTitle(action);
	}
	
	
	/**
	 * Returns table value
	 */
	static string getTableValue
	(
		Layout layout, TableModel model,
		int row_number, FieldInfo field, Dict item
	)
	{
		if (field.name == "source")
		{
			return static::getSourceColumn(item);
		}
		else if (field.name == "dest")
		{
			return static::getDestColumn(item);
		}
		return "";
	}
	
	
	/**
	 * Returns source
	 */
	static string getSourceColumn(Dict item)
	{
		string res = "";
		string domain_name = "";
		
		res ~= item["domain_name"];
		
		if (item["source_port"] != 80)
		{
			res ~= ":" ~ item["source_port"];
		}
		
		if (item["route_prefix"] != "/")
		{
			res ~= item["route_prefix"];
		}
		
		return res;
	}
	
	
	/**
	 * Returns dest
	 */
	static string getDestColumn(Dict item)
	{
		string res = "";
		
		res ~= item["docker_name"];
		
		if (item["target_port"] != 80)
		{
			res ~= ":" ~ item["target_port"];
		}
		
		if (item["target_prefix"] != "/")
		{
			res ~= item["target_prefix"];
		}
		
		return res;
	}
	
	
}