/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Backend.Api;

use Runtime.ORM.Connection;
use Runtime.ORM.Relation;
use Runtime.ORM.Query;
use Runtime.ORM.QueryField;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.QueryResult;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Crud.SaveRelationApi;
use Runtime.Widget.Crud.Rules.CrudRule;
use Runtime.Widget.Crud.Rules.LowerCase;
use Runtime.Widget.Crud.Rules.Purify;
use Runtime.Widget.Crud.Rules.ReadOnly;
use Runtime.Widget.Crud.Rules.Required;
use Runtime.Widget.Crud.Rules.Unique;
use Bayrell.CloudOS.Backend.Lib.AppXml;


class ApplicationSave extends SaveRelationApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "app.applications::save";
	
	
	/**
	 * Returns table name
	 */
	string getTableName() => "applications";
	
	
	/**
	 * Constructor
	 */
	void constructor()
	{
		parent();
		
		/* Create xml */
		this.xml = new AppXml();
		this.xml.connection = this.getConnection();
	}
	
	
	/**
	 * Returns rules
	 */
	Vector<CrudRule> getRules() =>
	[
		/* Stack name */
		new Purify{ "name": "stack_name" },
		new LowerCase{ "name": "stack_name" },
		new Required{ "name": "stack_name" },
		
		/* Name */
		new Purify{ "name": "name" },
		new LowerCase{ "name": "name" },
		new Required{ "name": "name" },
		
		/* Unique */
		new Unique{"keys": ["stack_name", "name"]},
	];
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() =>
	[
		"id",
		"content",
		"custom_patch",
		"environments",
		"name",
		"stack_name",
		"status",
		"template_id",
		"template_uid",
		"template_name",
		"template_version",
		"template_version_id",
		"variables",
		"volumes",
		"yaml",
		"yaml_file_id",
		"count_work",
		"count_total",
		"docker_tasks",
		"gmtime_created",
		"gmtime_updated",
	];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() =>
	[
		"custom_patch",
		"environments",
		"name",
		"stack_name",
		"template_version_id",
		"volumes",
	];
	
	
	/**
	 * Returns query field
	 */
	QueryField getQueryField(string table_name, string field_name)
	{
		if (field_name == "template_version")
			return new QueryField("templates_versions", "version", field_name);
		
		if (field_name == "template_name")
			return new QueryField("templates", "name", field_name);
		
		if (field_name == "template_id")
			return new QueryField("templates", "id", field_name);
		
		if (field_name == "template_uid")
			return new QueryField("templates", "uid", field_name);
		
		if (field_name == "count_work")
			return new QueryField("docker_services", "count_work", field_name);
		
		if (field_name == "count_total")
			return new QueryField("docker_services", "count_total", field_name);
		
		if (field_name == "docker_tasks")
			return new QueryField("docker_services", "docker_tasks", field_name);
		
		return parent(table_name, field_name);
	}
	
	
	/**
	 * Build search query
	 */
	async void buildSearchQuery(Query q)
	{
		q = q
			.orderBy("applications.stack_name", "asc")
			.orderBy("applications.name", "asc")
			
			.leftJoin(
				"templates_versions",
				[
					"applications.template_version_id=templates_versions.id"
				]
			)
			
			.leftJoin(
				"templates",
				[
					"templates_versions.template_id=templates.id"
				]
			)
			
			.leftJoin(
				"docker_services",
				[
					"applications.stack_name=docker_services.stack_name",
					"applications.name=docker_services.service_name",
					"docker_services.enable=1",
					"docker_services.is_deleted=0",
				]
			)
		;
	}
	
	
	/**
	 * After save
	 */
	async void onSaveAfter()
	{
		await parent();
		
		Map item_variables = {};
		
		/* Save variables */
		Collection variables = this.post_data["item", "variables"];
		if (variables)
		{
			for (int i=0; i<variables.count(); i++)
			{
				Dict variable = variables.get(i);
				string variable_name = variable.get("name");
				
				if (variable_name == "_var_app_name_") continue;
				if (variable_name == "_var_service_name_") continue;
				
				item_variables.set(
					variable.get("name"),
					variable.get("value")
				);
			}
		}
		
		/* Set variables */
		this.item.set("variables", item_variables);
		
		/* Build xml and yaml */
		this.xml.setItem(this.item);
		await this.xml.buildContent(this.pk == null ? "create" : "edit");
		
		/* Save item */
		this.item.set("content", this.xml.xml_content);
		this.item.set("yaml", this.xml.yaml_content);
		this.item.save(this.getConnection());
	}
	
	
	/**
	 * Build variables
	 */
	async void buildVariables()
	{
		if (this.xml.item == null)
		{
			this.xml.setItem(this.item);
			await this.xml.buildContent("load");
		}
		
		Map data_item = this.result.data.get("item");
		
		/* Set patches */
		Collection patches = this.xml.getPatches();
		data_item.set("patches", patches);
		
		/* Set variables */
		data_item.set("variables", this.xml.variables);
		if (data_item.get("variables") == null)
		{
			data_item.set("variables", []);
		}
		
		/* Set environments if null */
		if (data_item.get("environments") == null)
		{
			data_item.set("environments", []);
		}
		
		/* Set volumes if null */
		if (data_item.get("volumes") == null)
		{
			data_item.set("volumes", []);
		}
	}
	
	
	/**
	 * Action item
	 */
	@ApiMethod
	async void actionItem()
	{
		await parent();
		await this.buildVariables();
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod
	async void actionSave()
	{
		await parent();
		await this.buildVariables();
	}
	
	
	/**
	 * Action delete
	 */
	@ApiMethod
	async void actionDelete()
	{
		await parent();
	}
}