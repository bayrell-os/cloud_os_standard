/*!
 *  Bayrell Cloud OS
 *
 *  (c) Copyright 2020-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.CloudOS.Components.Routes;

use Runtime.re;
use Runtime.DateTime;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.Relation;
use Runtime.Widget.Crud.Api.CrudApi;
use Runtime.Widget.Crud.Api.CrudRule;
use Runtime.Widget.Crud.Api.Dictionary;
use Runtime.Widget.Crud.Api.LowerCase;
use Runtime.Widget.Crud.Api.Purify;
use Runtime.Widget.Crud.Api.Required;
use Runtime.Widget.Crud.Api.Unique;


class RouteApi extends CrudApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "app.route";
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "routes";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		if (api_name == "crud.search") return "actionSearch";
		if (api_name == "crud.search_one") return "actionSearchOne";
		if (api_name == "crud.dictionary") return "actionDictionary";
		if (api_name == "crud.item") return "actionItem";
		if (api_name == "crud.save") return "actionSave";
		if (api_name == "crud.delete") return "actionDelete";
		return "";
	}
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() => [
		"id",
		"enable",
		"protocol",
		"domain_id",
		"domain_name",
		"route_prefix",
		"docker_name",
		"source_port",
		"target_port",
		"target_prefix",
		"nginx_config",
		"gmtime_created",
		"gmtime_updated",
	];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() => [
		"enable",
		"protocol",
		/*"protocol_data",*/
		"domain_id",
		"route_prefix",
		"docker_name",
		"source_port",
		"target_port",
		"target_prefix",
		"nginx_config",
	];
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
		new Purify{ "name": "docker_name" },
		new LowerCase{ "name": "docker_name" },
		new Required{ "name": "docker_name" },
		new Required{ "name": "domain_id" },
		new Required{ "name": "enable" },
		new Required{ "name": "protocol" },
		
		/* ForeignKey */
		new Dictionary{
			"dictionary_name": "domains",
			"table_name": "domains",
		},
	];
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q)
	{
		int domain_id = (int)this.post_data["domain_id"];
		if (domain_id > 0)
		{
			q = q.where("routes.domain_id", "=", domain_id)
		}
		
		q.leftJoin("domains", [
			"domains.id = routes.domain_id",
		]);
		
		q = q
			.addFields([
				"domains.domain_name as domain_name"
			])
			.orderBy("domains.domain_name", "asc")
			.orderBy("routes.route_prefix", "asc")
			.orderBy("routes.id", "desc")
		;
		
		return q;
	}
	
	
	/**
	 * Validate item
	 */
	async Dict onSaveBefore(Relation item, Dict update_data)
	{
		await parent::onSaveBefore(item, update_data);
		
		int domain_id = (int)this.post_data["domain_id"];
		if (domain_id > 0)
		{
			item.set("domain_id", domain_id);
		}
	}
	
}